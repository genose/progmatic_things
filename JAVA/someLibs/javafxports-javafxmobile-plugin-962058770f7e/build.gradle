plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id 'com.gradle.build-scan' version '2.1'
    id 'java-gradle-plugin'
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply from: 'mavenPublish.gradle'

group = 'org.javafxports'
version = '1.4.0-SNAPSHOT'

buildScan {
    termsOfServiceUrl   = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}


ext.publish = !project.version.endsWith('SNAPSHOT')
if (!hasProperty('bintrayUser') || !hasProperty('bintrayKey')) {
    ext.bintrayUser = ''
    ext.bintrayKey = ''
}

repositories {
    google()
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:3.2.1'
    compile 'com.gluonhq:robovm-dist-compiler:2.3.1-ios11'
    testCompile localGroovy()
    testCompile 'junit:junit:4.8.2'
}

configurations {
    published
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives sourceJar, groovydocJar
}

bintray {
    user = bintrayUser
    key = bintrayKey

    configurations = [ 'published', 'archives' ]
    dryRun = !project.publish
    publish = project.publish
    pkg {
        repo = 'maven'
        name = project.name
        desc = 'The jfxmobile-plugin is a gradle plugin that unifies the building of JavaFX applications for three different target platforms: android, ios and desktop.'
        websiteUrl = 'http://javafxports.org'
        issueTrackerUrl = 'https://bitbucket.org/javafxports/javafxmobile-plugin/issues'
        vcsUrl = 'https://bitbucket.org/javafxports/javafxmobile-plugin'
        licenses = [ 'BSD 3-Clause' ]
        labels = [ 'ios', 'android', 'mobile', 'javafx', 'desktop', 'gradle', 'plugin' ]
        version {
            name = project.version
            vcsTag = project.version
            attributes = [ 'gradle-plugin' : "org.javafxports.jfxmobile:${project.group}:${project.name}" ]
        }
    }
}
